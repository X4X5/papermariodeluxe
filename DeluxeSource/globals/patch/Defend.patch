%@Data:Battle 802AD624 { 000000FE }
%802A4EE8 A button check when in strategies menu
@Function:Battle 802A4654 {
	ADDIU v0, r0, 00FE %set text opacity to FE, looks much better on emu
}

@Hook:Battle 802A7B68 {
	PUSH t0, t1
	SW	T0, 0000 (V1)
	LW	V0, 000C (A0)
	LIO t0, 802AD690
	ORI t1, r0, 0001
	SW t1, 0000 (t0)
	SW t1, 0004 (t0)
	SW t1, 0008 (t0)
	SW t1, 000C (t0)
	POP t0, t1
	J 802A7B70
	NOP
}

@Function:Battle 802A7BA4 {
	SW v0, 0004 (a3) %now overwrites the new location of run away rather than overwriting defend
	SW r0, 0004 (v1)
}

@Function 41C950 % (802A7BC0) Loads routine in battle in the strategies menu that determines how many commands are available for Mario.
{
	ADDIU S4, S4, 0002 % Now adds a 4th option for Mario so he can 'Defend' in battle.
}

#new:Function $GetPartnerCount { %excludes goompa
	LIO t0, 8010F2AC %first partner bool
	DADDU v0, r0, r0 %partner count
	DADDU v1, r0, r0 %loop count
	ORI t2, r0, 0004 %goompa loop count index (goombario 0, kooper 1, etc.)
	.loop
	LB t1, 0000 (t0) %load partner bool
	BNEL t1, r0, .hasPartner %if true, 1 is added to v0
	ADDIU v0, v0, 0001
	.hasPartner
	ADDIU v1, v1, 0001 %add 1 to partner loop count
	ADDIU t0, t0, 0008 %increment partner bool pointer to next partner
	BNE v1, t2, .notGoompa
	NOP
	ADDIU t0, t0, 0008 %increment partner bool pointer to next partner
	ADDIU v1, v1, 0001 %add 1 if goompa
	.notGoompa
	SLTI t3, v1, 0009
	BNE t3, r0, .loop
	NOP
	JR RA
	NOP
}

/%
list of icon pointers
802AD640

list of text IDs
802AD658

list of optionIDs
802AD678

list of bottom text IDs
802AD6C0

list of enabled options
802AD690
%/

%run away = 3
%defend = 4
%change member = 5
%do nothing = 9

%func_802A5738 draws bottom text box
%Table of partner icon pointers is at 8008F000
%multiply partner index by 8 to get ptr to active partner icon

#new:Data $StrategyTable {
	%icon ptr 	%string ptr 		%option ID 	%string bottom text ptr
	802922A0	$DoNothingString	00000009	$DoNothingStringBottom
	802921B0 	$DefendString 		00000004 	$DefendStringBottom
	80292160	$RunAwayString		00000003	$RunAwayStringBottom
	00000000 	00000000			00000000	00000000
}

#new:String $ChangeMemberString {
	Change Member[END]
}

#new:String $DoNothingString {
	Do Nothing[END]
}

#new:String $DefendString {
	Defend[END]
}

#new:String $RunAwayString {
	Run Away[END]
}

#new:String $ChangeMemberStringBottom {
	You'll change your party member![END]
}

#new:String $DoNothingStringBottom {
	You won't do anything during[BR]
	this turn![END]
}

#new:String $DefendStringBottom {
	You'll receive 1 extra defense[BR]
	during this turn![END]
}

#new:String $RunAwayStringBottom {
	You'll run away from the battle![END]
}

@Hook 41C91C { % 802A7B8C
	PUSH s0, s1, s2, s3, s4, t0, t1, t2, v0, v1, ra
	LIO s0, 802AD640 %icon pointers
	LIO s1, 802AD658 %text pointers/text IDs (we are changing them from IDs to pointers)
	LIO s2, 802AD678 %option IDs
	LIO s3, 802AD6C0 %bottom text pointers/text IDs (we are changing them from IDs to pointers)
	JAL $GetPartnerCount
	NOP
	SLTI t0, v0, 2 %if partner count is less than 2, change member option doesn't appear
	LIO s4, $StrategyTable
	BNE t0, r0, .afterPartnerCheck
	NOP
	%increment s0-s3 because change member is present and we dont want mess with this option
	ADDIU s0, s0, 0004
	ADDIU s1, s1, 0004
	ADDIU s2, s2, 0004
	ADDIU s3, s3, 0004
	.afterPartnerCheck
	.variationCopyLoop
	LW t0, 0000 (s4)
	BEQ t0, r0, .end
	NOP
	SW t0, 0000 (s0) %store icon ptr
	LW t0, 0004 (s4)
	SW t0, 0000 (s1) %store string ptr
	LW t0, 0008 (s4)
	SW t0, 0000 (s2) %store option ID
	LW t0, 000C (s4)
	SW t0, 0000 (s3) %store bottom str ptr
	ADDIU s0, s0, 0004
	ADDIU s1, s1, 0004
	ADDIU s2, s2, 0004
	ADDIU s3, s3, 0004
	BEQ r0, r0, .variationCopyLoop
	ADDIU s4, s4, 0010 %size of StrategyTable line
	.end
	POP s0, s1, s2, s3, s4, t0, t1, t2, v0, v1, ra
	LUI v0, 800E
	J 802A7B94
	LW v0, C074 (v0)
}

@Function 18F40C % (80260B2C) Sets animation for 'Defend' to use the moment you press accept on it.
{
	ORI	A2, A2, 0004 % Set to 0004 so it's Mario's 'Walking' animation. This fixes nearly ALL the animation issues.
}

@Data 1B1BB8 % (802832D4) Loads a table for Mario's animations? (Uncertain what this is)
{
	00010014 00000000 % Set it to 00010014 00000000 instead of 00010014 0000001A and Mario will have the proper defense animation!
}

@Data 1B482C % (80285F4C) Loads a table for Mario's animations? (Uncertain what this is) (Code was loaded at 802C57A4)
{
	00010014 00000046 % Set it to 00010014 instead of 00010002 so now Defend always plays the Defend pose unless they are damaged.
}
